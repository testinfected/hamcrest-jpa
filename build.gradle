group 'com.vtence.hamcrest'
version '2.0.0-SNAPSHOT'

apply plugin: 'java'
sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenCentral()
}

def libs = [
        // We need a real api, not stubs
        jpa_api       : 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final',
        hamcrest      : 'org.hamcrest:java-hamcrest:2.0.0.0',
        junit         : 'junit:junit:4.12@jar',
        hamcrest_junit: 'org.hamcrest:hamcrest-junit:2.0.0.0',
]

def providedDependencies = []
ext {
    provided = { providedDependencies << it; it }
}


dependencies {
    compile libs.hamcrest
    compile libs.jpa_api, provided

    testCompile libs.junit
    testCompile libs.hamcrest_junit
}

compileJava.options.encoding = 'UTF-8'

task 'version' << {
    println version
}

modifyPom {
    project {
        name 'JPA Matchers'
        description 'Hamcrest matchers library for JPA'
        url 'https://github.com/testinfected/hamcrest-jpa'
        packaging 'jar'
        licenses {
            license {
                name 'BSD License'
                url 'https://opensource.org/licenses/BSD-3-Clause'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/testinfected/hamcrest-jpa'
            connection 'scm:git:https://github.com/testinfected/hamcrest-jpa.git'
            developerConnection 'https://github.com/testinfected/hamcrest-jpa.git'
        }
        developers {
            developer {
                id 'testinfected'
                name 'Vincent TencÃ©'
            }
        }

        providedDependencies.each { dep ->
            dependencies.find { it.artifactId == dep.name }.scope = 'provided'
        }

        // no need to publish test dependencies
        dependencies.removeAll { it.scope == "test" }
    }
}

